# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

test("platform-bus") {
  sources = [
    "main.cc",
  ]
  deps = [
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/devmgr-integration-test",
    "$zx/system/ulib/devmgr-launcher",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/fdio",
    "$zx/system/ulib/libzbi",
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zx",
    "$zx/system/ulib/zxtest",
  ]
}

testonly = true

simple_drivers = [
  "child-1",
  "child-2",
  "child-2.proxy",
  "child-3",
  "parent",
  "composite",
]

group("test") {
  deps = [
    ":test-audio-codec",
    ":test-board",
    ":test-clock",
    ":test-gdc",
    ":test-gpio",
    ":test-i2c",
    ":test-power",
  ]
  foreach(driver, simple_drivers) {
    deps += [ ":test-$driver" ]
  }
}

driver("test-board") {
  sources = [
    "test-audio-codec.cc",
    "test-board.cc",
    "test-clock.cc",
    "test-gdc.cc",
    "test-gpio.cc",
    "test-i2c.cc",
    "test-power.cc",
    "test.cc",
  ]
  deps = [
    "$zx/system/banjo/ddk.protocol.platform.bus",
    "$zx/system/banjo/ddk.protocol.platform.device",
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/ddktl",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/zircon",
  ]
}

driver("test-gpio") {
  sources = [
    "test/gpio.cc",
  ]
  deps = [
    "$zx/system/banjo/ddk.protocol.gpio",
    "$zx/system/banjo/ddk.protocol.gpioimpl",
    "$zx/system/banjo/ddk.protocol.platform.bus",
    "$zx/system/banjo/ddk.protocol.platform.device",
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/ddktl",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zx",
  ]
}

driver("test-power") {
  sources = [
    "test/power.cc",
  ]
  deps = [
    "$zx/system/banjo/ddk.protocol.platform.bus",
    "$zx/system/banjo/ddk.protocol.platform.device",
    "$zx/system/banjo/ddk.protocol.power",
    "$zx/system/banjo/ddk.protocol.powerimpl",
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/ddktl",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zx",
  ]
}

driver("test-clock") {
  sources = [
    "test/clock.cc",
  ]
  deps = [
    "$zx/system/banjo/ddk.protocol.clock",
    "$zx/system/banjo/ddk.protocol.clockimpl",
    "$zx/system/banjo/ddk.protocol.platform.bus",
    "$zx/system/banjo/ddk.protocol.platform.device",
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/ddktl",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zx",
  ]
}

driver("test-i2c") {
  sources = [
    "test/i2c.cc",
  ]
  deps = [
    "$zx/system/banjo/ddk.protocol.i2c",
    "$zx/system/banjo/ddk.protocol.i2cimpl",
    "$zx/system/banjo/ddk.protocol.platform.bus",
    "$zx/system/banjo/ddk.protocol.platform.device",
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/ddktl",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zx",
  ]
}

driver("test-audio-codec") {
  sources = [
    "test/audio-codec.cc",
  ]
  deps = [
    "$zx/system/banjo/ddk.protocol.codec",
    "$zx/system/banjo/ddk.protocol.platform.bus",
    "$zx/system/banjo/ddk.protocol.platform.device",
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/ddktl",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zx",
  ]
}

driver("test-gdc") {
  sources = [
    "test/gdc.cc",
  ]
  deps = [
    "$zx/system/banjo/ddk.protocol.gdc",
    "$zx/system/banjo/ddk.protocol.platform.bus",
    "$zx/system/banjo/ddk.protocol.platform.device",
    "$zx/system/ulib/ddk",
    "$zx/system/ulib/ddktl",
    "$zx/system/ulib/fbl",
    "$zx/system/ulib/zircon",
    "$zx/system/ulib/zx",
  ]
  public_deps = [
    "$zx/system/fidl/fuchsia-sysmem:c",
  ]
}

foreach(driver, simple_drivers) {
  driver("test-$driver") {
    sources = [
      "test/$driver.c",
    ]
    public_deps = [
      "$zx/system/fidl/fuchsia-sysmem:c",
    ]
    deps = [
      "$zx/system/banjo/ddk.protocol.clock",
      "$zx/system/banjo/ddk.protocol.codec",
      "$zx/system/banjo/ddk.protocol.composite",
      "$zx/system/banjo/ddk.protocol.gdc",
      "$zx/system/banjo/ddk.protocol.gpio",
      "$zx/system/banjo/ddk.protocol.platform.device",
      "$zx/system/banjo/ddk.protocol.power",
      "$zx/system/dev/lib/device-protocol-i2c",
      "$zx/system/ulib/ddk",
      "$zx/system/ulib/sync",
      "$zx/system/ulib/zircon",
    ]
  }
}
