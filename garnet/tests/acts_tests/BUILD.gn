# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/dart/test.gni")
import("//build/testing/environments.gni")

specs = [
  {
    name = "acts_bt1_test"
    tags = [ "acts-connectivity-bt1" ]
    testbed = "bt_rf_chamber_nuc_phone"
    tests = "BleFuchsiaAndroidTest GattServerSetupTest NetstackFuchsiaTest"
    timeout_minutes = 15
  },
  {
    name = "acts_wifi_test"
    tags = [ "acts-connectivity-wifi" ]
    testbed = "wifi_rf_chamber_nuc"
    tests = "WlanScanTest WlanPhyComplianceABGTest WlanSecurityComplianceABGTest WlanPhyCompliance11NTest"
    timeout_minutes = 1070  # 18 hours less 10 minutes for output processing in Dart script
  },
]

test_labels = []

foreach(spec, specs) {
  dart_test(spec.name) {
    sources = [
      "acts_test.dart",
    ]

    deps = [
      "//third_party/dart-pkg/pub/archive",
      "//third_party/dart-pkg/pub/test",
    ]

    environments = [
      {
        dimensions = {
          testbed = spec.testbed
          pool = "fuchsia.tests.connectivity"
        }

        tags = spec.tags
      },
    ]

    args = [
      "--timeout=${spec.timeout_minutes}",
      "--tests=${spec.tests}",
    ]
  }
  test_labels += [ ":${spec.name}($host_toolchain)" ]
}

group("acts_tests") {
  testonly = true
  deps = test_labels
}
