# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/protobuf/proto_library.gni")

source_set("headers") {
  sources = [
    "dockyard.h",
    "dockyard_service_impl.h",
  ]

  public_deps = [
    ":protos",
    "//garnet/lib/system_monitor:lib",
    "//third_party/grpc:grpc++",
  ]
}

source_set("lib") {
  sources = [
    "dockyard.cc",
    "dockyard_service_impl.cc",
    "test_sample_generator.cc",
    "test_sample_generator.h",
  ]

  public_deps = [
    ":headers",
  ]
}

proto_library("protos") {
  sources = [
    "../protos/dockyard.proto",
  ]

  generate_python = false
  cc_generator_options = "lite"
  generator_plugin_suffix = ".grpc.pb"
  generator_plugin_label = "//third_party/grpc:grpc_cpp_plugin"

  deps = [
    "//third_party/grpc:grpc++",
  ]
}

static_library("dockyard") {
  output_name = "dockyard"
  complete_static_lib = true

  deps = [
    ":lib",
  ]
}

group("libdockyard") {
  public_deps = [
    ":dockyard($host_toolchain)",
  ]
}

source_set("tests") {
  testonly = true

  sources = [
    "dockyard_test.cc",
  ]

  deps = [
    ":lib",
    "//third_party/googletest:gtest",
  ]
}
