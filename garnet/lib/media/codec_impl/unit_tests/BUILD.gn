# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")
import("//build/test/test_package.gni")

group("unit_tests") {
  testonly = true
  deps = [
    ":codec_impl_unit_tests",
    ":decryptor_adapter_tests",
  ]
}

executable("codec_impl_unit_tests_bin") {
  testonly = true
  output_name = "codec_impl_unit_tests"

  sources = []

  deps = [
    ":codec_impl_tests",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gtest",
  ]
}

test_package("codec_impl_unit_tests") {
  deps = [
    ":codec_impl_unit_tests_bin",
  ]

  tests = [
    {
      name = "codec_impl_unit_tests"
      environments = basic_envs
    },
  ]
}

source_set("codec_impl_tests") {
  testonly = true

  sources = [
    "test_failures.cc",
    "test_lifetime.cc",
  ]

  deps = [
    "//garnet/lib/media/codec_impl",
    "//garnet/lib/media/codec_impl/test_utils",
    "//garnet/public/lib/gtest",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/zx",
  ]
}

executable("decryptor_adapter_tests_bin") {
  testonly = true
  output_name = "decryptor_adapter_tests"

  sources = [
    "test_decryptor_adapter.cc",
  ]

  deps = [
    "//garnet/lib/media/codec_impl",
    "//garnet/public/lib/fxl/test:gtest_main",
    "//sdk/lib/sys/cpp/testing:integration",
    "//third_party/googletest:gtest",
    "//zircon/public/fidl/fuchsia-sysmem",
    "//zircon/public/lib/async-loop-cpp",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/zx",
  ]
}

test_package("decryptor_adapter_tests") {
  deps = [
    ":decryptor_adapter_tests_bin",
  ]

  tests = [
    {
      name = "decryptor_adapter_tests"
      environments = basic_envs
    },
  ]
}
