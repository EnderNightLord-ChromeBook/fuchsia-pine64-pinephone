# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/test/test_package.gni")
import("//build/testing/environments.gni")
import("//garnet/bin/appmgr/integration_tests/sandbox/sandbox_test_package.gni")

group("storage") {
  testonly = true
  deps = [
    ":has_isolated_cache_storage",
    ":has_isolated_persistent_storage",
    ":isolated_persistent_storage",
    ":no_persistent_storage",
  ]
}

sandbox_test_package("no_persistent_storage") {
  sources = [
    "no_persistent_storage.cc",
  ]

  deps = [
    "//garnet/bin/appmgr/integration_tests/sandbox:namespace_test",
    "//src/lib/fxl/test:gtest_main",
  ]
  environments = basic_envs
}

sandbox_test_package("has_isolated_persistent_storage") {
  sources = [
    "has_persistent_storage.cc",
  ]

  deps = [
    "//garnet/bin/appmgr/integration_tests/sandbox:namespace_test",
    "//src/lib/fxl/test:gtest_main",
  ]
  environments = basic_envs
}

sandbox_test_package("has_isolated_cache_storage") {
  sources = [
    "has_isolated_cache_storage.cc",
  ]

  deps = [
    "//garnet/bin/appmgr/integration_tests/sandbox:namespace_test",
    "//sdk/fidl/fuchsia.sys.test",
    "//src/lib/fxl/test:gtest_main",
    "//third_party/googletest:gmock",
  ]
  environments = basic_envs
}

executable("isolated_persistent_storage_bin") {
  testonly = true
  output_name = "isolated_persistent_storage"

  sources = [
    "isolated_persistent_storage.cc",
  ]

  deps = [
    "//garnet/bin/appmgr/integration_tests/util:data_file_reader_writer",
    "//garnet/bin/appmgr/integration_tests/util:data_file_reader_writer_util",
    "//garnet/public/lib/gtest",
    "//sdk/lib/sys/cpp/testing:integration",
    "//src/lib/fxl/test:gtest_main",
    "//zircon/public/lib/async-loop-cpp",
  ]
}

test_package("isolated_persistent_storage") {
  tests = [
    {
      name = "isolated_persistent_storage"
      environments = basic_envs
    },
  ]

  deps = [
    ":isolated_persistent_storage_bin",
    "//garnet/bin/appmgr/integration_tests/util:persistent_storage_test_util",
  ]
}
