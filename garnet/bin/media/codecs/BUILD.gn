# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("codecs") {
  testonly = true
  deps = [
    ":chunk_input_stream",
    ":mpsc_queue",
    ":output_sink",
    ":timestamp_extrapolator",
    "sw",
    "test",
  ]
}

group("tests") {
  testonly = true
  deps = [
    "test",
    "//garnet/examples/media:use_h264_decoder_test",
  ]
}

config("local_header_include_config") {
  include_dirs = [ "." ]
}

source_set("timestamp_extrapolator") {
  public = [
    "timestamp_extrapolator.h",
  ]
  sources = [
    "timestamp_extrapolator.cc",
  ]
  public_configs = [ ":local_header_include_config" ]
  public_deps = [
    "//zircon/public/lib/zx",
  ]
}

source_set("mpsc_queue") {
  visibility = [ "//garnet/bin/media/codecs/*" ]

  public = [
    "mpsc_queue.h",
  ]

  public_configs = [ ":local_header_include_config" ]
}

source_set("chunk_input_stream") {
  public = [
    "chunk_input_stream.h",
  ]
  sources = [
    "chunk_input_stream.cc",
  ]
  public_configs = [ ":local_header_include_config" ]
  public_deps = [
    ":timestamp_extrapolator",
    "//garnet/lib/media/codec_impl",
    "//zircon/public/lib/zx",
  ]
}

source_set("output_sink") {
  public = [
    "output_sink.h",
  ]
  sources = [
    "output_sink.cc",
  ]
  public_configs = [ ":local_header_include_config" ]
  public_deps = [
    "//garnet/bin/media/codecs:mpsc_queue",
    "//garnet/lib/media/codec_impl",
    "//zircon/public/lib/fit",
    "//zircon/public/lib/zx",
  ]
}
