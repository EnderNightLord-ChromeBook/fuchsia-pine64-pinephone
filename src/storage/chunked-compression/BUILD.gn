# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/host.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

static_library("chunked-compression") {
  sources = [
    "chunked-archive.cc",
    "chunked-archive.h",
    "chunked-compressor.cc",
    "chunked-compressor.h",
    "chunked-decompressor.cc",
    "chunked-decompressor.h",
    "status.h",
  ]
  public_deps = [
    "//src/lib/fxl",
    "//zircon/public/lib/fbl",
  ]
  deps = [ "//third_party/zstd" ]
}

executable("chunked-compress-bin") {
  output_name = "chunked-compress"
  sources = [ "main.cc" ]
  deps = [
    ":chunked-compression",
    "//src/lib/fxl",
    "//zircon/public/lib/fbl",
  ]
}

install_host_tools("chunked-compress-host") {
  deps = [ ":chunked-compress-bin" ]
  outputs = [ "chunked-compress" ]
}

executable("chunked-compression-unittests") {
  testonly = true
  output_name = "chunked-compression-unittests"
  sources = [
    "chunked-compressor-test.cc",
    "chunked-decompressor-test.cc",
  ]
  deps = [
    ":chunked-compression",
    "//src/lib/fxl/test:gtest_main",
    "//zircon/public/lib/pretty",
  ]
}

test_package("chunked-compression-unittests-pkg") {
  deps = [ ":chunked-compression-unittests" ]
  tests = [
    {
      name = "chunked-compression-unittests"
      environments = basic_envs
    },
  ]
}

group("tests") {
  testonly = true
  deps = [ ":chunked-compression-unittests-pkg" ]
}
