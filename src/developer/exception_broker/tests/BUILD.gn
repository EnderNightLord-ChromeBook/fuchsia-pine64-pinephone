# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be found in the LICENSE file.

import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

group("tests") {
  testonly = true

  public_deps = [
    ":exception_broker_tests",

    # Packages for the components that we are injecting in the integration tests.
    "//garnet/bin/logger",
    "//src/developer/exception_broker",
  ]
}

test_package("exception_broker_tests") {
  tests = [
    {
      name = "exception_broker_unittest"
    },
    {
      name = "exception_broker_integration_test"
    },
  ]

  # These are binaries needed by some tests. Mainly used to trigger exceptions.
  binaries = [
    {
      name = "exception_broker_crasher"
    },
  ]

  deps = [
    ":crasher",
    ":exception_broker_integration_test",
    ":exception_broker_unittest",
  ]
}

# Utilities used for testing purposes.
source_set("test_lib") {
  testonly = true

  sources = [
    "crasher_wrapper.cc",
    "crasher_wrapper.h",
  ]

  deps = [
    "//garnet/public/lib/syslog/cpp",
    "//zircon/public/lib/fdio",
    "//zircon/public/lib/zx",
  ]
}

executable("exception_broker_unittest") {
  testonly = true

  sources = [
    "exception_broker_unittest.cc",
  ]

  deps = [
    ":test_lib",
    "//sdk/lib/sys/cpp/testing:unit",
    "//src/developer/exception_broker:lib",
    "//src/lib/fxl",
    "//src/lib/fxl/test:test_settings",
    "//third_party/googletest:gtest",
    "//zircon/public/lib/zx",
  ]
}

executable("exception_broker_integration_test") {
  testonly = true

  sources = [
    "exception_broker_integration_test.cc",
  ]

  deps = [
    ":test_lib",
    "//src/developer/exception_broker:lib",
    "//src/lib/fxl",
    "//src/lib/fxl/test:test_settings",
    "//third_party/googletest:gtest",
  ]
}

executable("crasher") {
  testonly = true
  output_name = "exception_broker_crasher"

  sources = [
    "crasher.cc",
  ]
}
