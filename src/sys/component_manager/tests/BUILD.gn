# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package/component.gni")
import("//build/rust/rustc_binary.gni")
import("//build/rust/rustc_library.gni")
import("//build/rust/rustc_test.gni")
import("//build/test/test_package.gni")

rustc_library("test_utils") {
  name = "test_utils"
  edition = "2018"
  source_root = "test_utils.rs"
  deps = [
    "//garnet/public/rust/fdio",
    "//garnet/public/rust/fuchsia-component",
    "//garnet/public/rust/fuchsia-runtime",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
  ]
}

rustc_library("hub_test_hook") {
  name = "hub_test_hook"
  edition = "2018"
  source_root = "hub_test_hook.rs"
  deps = [
    "//garnet/lib/rust/cm_rust",
    "//garnet/public/lib/fidl/rust/fidl",
    "//garnet/public/rust/fdio",
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-component",
    "//garnet/public/rust/fuchsia-runtime",
    "//garnet/public/rust/fuchsia-zircon",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
    "//src/sys/component_manager:lib",
    "//src/sys/component_manager/tests/fidl:fuchsia.test.hub-rustc",
    "//third_party/rust_crates:futures-preview",
    "//third_party/rust_crates:lazy_static",
  ]
}

rustc_test("hub_integration_test_bin") {
  name = "hub_integration_test"
  edition = "2018"
  source_root = "hub_integration_test.rs"
  deps = [
    ":hub_test_hook",
    ":test_utils",
    "//examples/components/routing/fidl:echo-rustc",
    "//garnet/lib/rust/cm_fidl_translator",
    "//garnet/lib/rust/cm_rust",
    "//garnet/lib/rust/io_util",
    "//garnet/public/lib/fidl/rust/fidl",
    "//garnet/public/rust/fdio",
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-component",
    "//garnet/public/rust/fuchsia-runtime",
    "//garnet/public/rust/fuchsia-vfs/pseudo-fs",
    "//garnet/public/rust/fuchsia-zircon",
    "//sdk/fidl/fuchsia.data:fuchsia.data-rustc",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2-rustc",
    "//src/sys/component_manager:lib",
    "//third_party/rust_crates:failure",
    "//third_party/rust_crates:futures-preview",
    "//third_party/rust_crates:log",
    "//third_party/rust_crates:url",
    "//zircon/public/fidl/fuchsia-io:fuchsia-io-rustc",
  ]
  non_rust_deps = [
    "//zircon/public/lib/async",
    "//zircon/public/lib/async-default",
    "//zircon/public/lib/async-loop",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/memfs",
  ]
}

test_package("hub_integration_test") {
  deps = [
    ":hub_integration_test_bin",
    ":mock_pkg_resolver_bin",
    "//examples/components/hub/hub_client",
    "//examples/components/routing/echo_server",
  ]

  meta = [
    {
      path = rebase_path("meta/mock_pkg_resolver_for_hub_integration_test.cmx")
      dest = "mock_pkg_resolver.cmx"
    },
    {
      path = rebase_path("//examples/components/routing/meta/echo_server.cml")
      dest = "echo_server.cm"
    },
    {
      path = rebase_path("//examples/components/hub/meta/hub_client.cml")
      dest = "hub_client.cm"
    },
    {
      path = rebase_path("meta/echo_realm_for_hub_integration.cml")
      dest = "echo_realm.cm"
    },
  ]

  binaries = [
    {
      name = "mock_pkg_resolver"
    },
    {
      name = "echo_server"
    },
    {
      name = "hub_client"
    },
  ]

  tests = [
    {
      name = "hub_integration_test"
    },
  ]
}

rustc_test("routing_integration_test_bin") {
  name = "routing_integration_test"
  edition = "2018"
  source_root = "routing_integration_test.rs"
  deps = [
    ":test_utils",
    "//garnet/lib/rust/cm_fidl_translator",
    "//garnet/public/rust/fdio",
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-component",
    "//garnet/public/rust/fuchsia-runtime",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
    "//third_party/rust_crates:failure",
  ]
}

rustc_test("storage_integration_test_bin") {
  name = "storage_integration_test"
  edition = "2018"
  source_root = "storage_integration_test.rs"
  deps = [
    ":test_utils",
    "//garnet/public/rust/fdio",
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-component",
    "//garnet/public/rust/fuchsia-runtime",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
    "//third_party/rust_crates:failure",
  ]
}

rustc_binary("memfs_bin") {
  name = "memfs"
  edition = "2018"
  source_root = "storage/memfs.rs"
  deps = [
    "//garnet/public/lib/fidl/rust/fidl",
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-runtime",
    "//garnet/public/rust/fuchsia-syslog",
    "//garnet/public/rust/fuchsia-vfs/pseudo-fs",
    "//garnet/public/rust/fuchsia-zircon",
    "//src/sys/component_manager:lib",
    "//third_party/rust_crates:failure",
    "//zircon/public/fidl/fuchsia-io:fuchsia-io-rustc",
  ]
  non_rust_deps = [
    "//zircon/public/lib/async",
    "//zircon/public/lib/async-default",
    "//zircon/public/lib/async-loop",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/memfs",
  ]
}

rustc_binary("storage_user_bin") {
  name = "storage_user"
  edition = "2018"
  source_root = "storage/storage_user.rs"
  deps = [
    "//garnet/lib/rust/io_util",
    "//garnet/public/lib/fidl/rust/fidl",
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-runtime",
    "//garnet/public/rust/fuchsia-syslog",
    "//garnet/public/rust/fuchsia-vfs/pseudo-fs",
    "//garnet/public/rust/fuchsia-zircon",
    "//src/sys/component_manager:lib",
    "//third_party/rust_crates:failure",
    "//zircon/public/fidl/fuchsia-io:fuchsia-io-rustc",
  ]
  non_rust_deps = [
    "//zircon/public/lib/async",
    "//zircon/public/lib/async-default",
    "//zircon/public/lib/async-loop",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/memfs",
  ]
}

rustc_binary("storage_realm_bin") {
  name = "storage_realm"
  edition = "2018"
  source_root = "storage/storage_realm.rs"
  deps = [
    "//garnet/lib/rust/files_async",
    "//garnet/lib/rust/io_util",
    "//garnet/public/lib/fidl/rust/fidl",
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-component",
    "//garnet/public/rust/fuchsia-syslog",
    "//garnet/public/rust/fuchsia-zircon",
    "//sdk/fidl/fuchsia.sys2:fuchsia.sys2-rustc",
    "//third_party/rust_crates:failure",
    "//zircon/public/fidl/fuchsia-io:fuchsia-io-rustc",
  ]
}

test_package("storage_integration_test") {
  deps = [
    ":memfs_bin",
    ":mock_pkg_resolver_bin",
    ":storage_integration_test_bin",
    ":storage_realm_bin",
    ":storage_user_bin",
  ]

  components =
      [ "//src/sys/component_manager:component_manager_as_v1_component" ]

  meta = [
    {
      path =
          rebase_path("meta/mock_pkg_resolver_for_storage_integration_test.cmx")
      dest = "mock_pkg_resolver.cmx"
    },
    {
      path = rebase_path("meta/storage/memfs.cml")
      dest = "memfs.cm"
    },
    {
      path = rebase_path("meta/storage/storage_user.cml")
      dest = "storage_user.cm"
    },
    {
      path = rebase_path("meta/storage/storage_realm.cml")
      dest = "storage_realm.cm"
    },
  ]

  binaries = [
    {
      name = "mock_pkg_resolver"
    },
    {
      name = "memfs"
    },
    {
      name = "storage_realm"
    },
    {
      name = "storage_user"
    },
  ]

  tests = [
    {
      name = "storage_integration_test"
    },
  ]
}

rustc_binary("mock_pkg_resolver_bin") {
  name = "mock_pkg_resolver"
  edition = "2018"
  source_root = "mock_pkg_resolver.rs"
  deps = [
    "//garnet/public/lib/fidl/rust/fidl",
    "//garnet/public/rust/fdio",
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-component",
    "//garnet/public/rust/fuchsia-syslog",
    "//garnet/public/rust/fuchsia-zircon",
    "//sdk/fidl/fuchsia.pkg:fuchsia.pkg-rustc",
    "//src/sys/lib/fuchsia_url:fuchsia_url",
    "//third_party/rust_crates:failure",
    "//third_party/rust_crates:futures-preview",
    "//third_party/rust_crates:lazy_static",
    "//zircon/public/fidl/fuchsia-io:fuchsia-io-rustc",
  ]
}

test_package("routing_integration_test") {
  deps = [
    ":mock_pkg_resolver_bin",
    ":routing_integration_test_bin",
    ":test_utils",
    "//examples/components/routing/echo_client",
    "//examples/components/routing/echo_server",
  ]

  components =
      [ "//src/sys/component_manager:component_manager_as_v1_component" ]

  meta = [
    {
      path =
          rebase_path("meta/mock_pkg_resolver_for_routing_integration_test.cmx")
      dest = "mock_pkg_resolver.cmx"
    },
    {
      path = rebase_path("//examples/components/routing/meta/echo_server.cml")
      dest = "echo_server.cm"
    },
    {
      path = rebase_path("//examples/components/routing/meta/echo_client.cml")
      dest = "echo_client.cm"
    },
    {
      path = rebase_path("meta/echo_realm.cml")
      dest = "echo_realm.cm"
    },
  ]

  binaries = [
    {
      name = "mock_pkg_resolver"
    },
    {
      name = "echo_server"
    },
    {
      name = "echo_client"
    },
  ]

  tests = [
    {
      name = "routing_integration_test"
    },
  ]
}

rustc_test("elf_runner_test_bin") {
  name = "elf_runner_test"
  edition = "2018"
  source_root = "elf_runner_test.rs"
  deps = [
    "//garnet/lib/rust/cm_fidl_translator",
    "//garnet/public/rust/fdio",
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-component",
    "//garnet/public/rust/fuchsia-runtime",
    "//garnet/public/rust/fuchsia-zircon",
    "//sdk/fidl/fuchsia.data:fuchsia.data-rustc",
    "//sdk/fidl/fuchsia.sys:fuchsia.sys-rustc",
    "//third_party/rust_crates:failure",
  ]
}

test_package("elf_runner_test") {
  deps = [
    ":elf_runner_test_bin",
    ":mock_pkg_resolver_bin",
    "//examples/components/basic:echo_args_bin",
  ]

  components =
      [ "//src/sys/component_manager:component_manager_as_v1_component" ]

  meta = [
    {
      path = rebase_path("meta/mock_pkg_resolver_for_elf_runner_test.cmx")
      dest = "mock_pkg_resolver.cmx"
    },
    {
      path = rebase_path("meta/echo_no_args.cml")
      dest = "echo_no_args.cm"
    },
    {
      path = rebase_path("//examples/components/basic/meta/echo_args.cml")
      dest = "echo_args.cm"
    },
  ]

  binaries = [
    {
      name = "mock_pkg_resolver"
    },
    {
      name = "echo_args"
    },
  ]

  tests = [
    {
      name = "elf_runner_test"
    },
  ]
}

rustc_test("no_pkg_resolver_test_bin") {
  name = "no_pkg_resolver_test"
  edition = "2018"
  source_root = "no_pkg_resolver_test.rs"
  deps = [
    "//garnet/public/rust/fuchsia-async",
    "//src/sys/component_manager:lib",
  ]
  non_rust_deps = [
    "//zircon/public/lib/async",
    "//zircon/public/lib/async-default",
    "//zircon/public/lib/async-loop",
    "//zircon/public/lib/sync",
    "//zircon/public/lib/memfs",
  ]
}

test_package("no_pkg_resolver_test") {
  deps = [
    ":no_pkg_resolver_test_bin",
  ]

  tests = [
    {
      name = "no_pkg_resolver_test"
    },
  ]
}

group("tests") {
  testonly = true

  deps = [
    ":elf_runner_test",
    ":hub_integration_test",
    ":no_pkg_resolver_test",
    ":routing_integration_test",
    ":storage_integration_test",
    "collections:collections_integration_test",
  ]
}
