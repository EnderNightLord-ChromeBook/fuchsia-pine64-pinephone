# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../../gn/build_rules.gni")

config("spinel_vk_public_config") {
  # The sources and clients use 'spinel_xxx.h' to include the headers.
  include_dirs = [ "${graphics_compute_dir}/spinel/include/spinel" ]
}

source_set("vk") {
  public = [
    "${graphics_compute_dir}/spinel/include/spinel/spinel_vk.h",
    "${graphics_compute_dir}/spinel/include/spinel/spinel_vk_types.h",

    #
    # FIXME(allanmac): I do not want to make allocator_device.h public
    # but there is an interaction between the friend=[] directive and
    # the host/device toolchain that appeared in a recent version of the
    # build tools.  FWIW, the plan is to move the allocator helpers out
    # of Spinel which will solve this issue in another way.
    #
    "allocator_device.h",
  ]
  include_dirs = [ "${graphics_compute_dir}/spinel" ]
  sources = [
    "${graphics_compute_dir}/spinel/include/spinel/spinel_vk.h",
    "${graphics_compute_dir}/spinel/include/spinel/spinel_vk_types.h",
    "allocator_device.c",
    "allocator_device.h",
    "block_pool.c",
    "block_pool.h",
    "composition_impl.c",
    "composition_impl.h",
    "core_vk.h",
    "device.c",
    "device.h",
    "dispatch.c",
    "dispatch.h",
    "expand_x.h",
    "handle_pool.c",
    "handle_pool.h",
    "path_builder_impl.c",
    "path_builder_impl.h",
    "queue_pool.c",
    "queue_pool.h",
    "raster_builder_impl.c",
    "raster_builder_impl.h",
    "render_impl.c",
    "render_impl.h",
    "ring.c",
    "ring.h",
    "spn_vk.c",          # FIXME(allanmac): rename spn_vk* -> vk*
    "spn_vk.h",          # FIXME(allanmac): rename spn_vk* -> vk*
    "spn_vk_layouts.h",  # FIXME(allanmac): rename spn_vk* -> vk*
    "spn_vk_target.h",   # FIXME(allanmac): rename spn_vk* -> vk*
    "styling_impl.c",
    "styling_impl.h",
  ]
  if (current_toolchain == host_toolchain) {
    defines = [ "SPN_VK_SHADER_INFO_AMD_STATISTICS" ]
  }
  deps = [
    "${graphics_compute_dir}/common",
    "${graphics_compute_dir}/common/vk",
    "${graphics_compute_dir}/hotsort/platforms/vk",
  ]
  public_deps = [
    "${graphics_compute_dir}/spinel",
    graphics_compute_vulkan_loader_target,
  ]
  public_configs = [ ":spinel_vk_public_config" ]
}

#
#
#
config("spinel_vk_ext_public_config") {
  include_dirs = [ "ext" ]
}

# A library used to load Spinel and Hotsort targets corresponding to a given
# Vulkan (vendor_id,device_id) tuple. For now, this statically links everything
# into the library. An alternative implementation may load the files from the
# filesystem, or customize the link to only include the targets required to
# run on the final device that will run the client application.
#
source_set("vk_find_target") {
  public = [
    "ext/spinel_vk_find_target.h",
  ]
  sources = [
    "ext/spinel_vk_find_target.c",
    "ext/spinel_vk_find_target.h",
  ]
  include_dirs = [
    "$root_gen_dir/src/graphics/lib/compute/spinel/platforms/vk",
    ".",  # Necessary for the spn_target.h includes in the source file.
  ]

  # Spinel targets.
  deps = [
    "targets/vendors/amd/gcn3:generate",
    "targets/vendors/intel/gen8:generate",
    "targets/vendors/nvidia/sm50:generate",
  ]
  public_configs = [ ":spinel_vk_ext_public_config" ]
}
