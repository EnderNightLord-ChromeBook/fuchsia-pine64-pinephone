# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/go/go_library.gni")
import("//build/go/go_test.gni")
import("//build/go/toolchain.gni")
import("//build/package.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")
import("//build/config/fuchsia/zircon_images.gni")

package("netstack") {
  deps = [
    "//garnet/go/src/grand_unified_binary",
  ]

  binaries = [
    {
      name = "netstack"
      source = "grand_unified_binary"
    },
  ]

  meta = [
    {
      path = rebase_path("meta/netstack.cmx")
      dest = "netstack.cmx"
    },
  ]
}

package("netstack_tools") {
  deps = [
    "ifconfig",
    "netstat",
  ]

  binaries = [
    {
      name = "ifconfig"
      shell = true
    },
    {
      name = "netstat"
      shell = true
    },
  ]
}

# A dep on a zircon_extras_manifest rule is only usable from within a package()
# target, so we declare netstack_netstat_test's manifest here and depend on it
# from netstack_netstat_test's enclosing test_package, `netstack_gotests`.
zircon_extras_manifest("netstack_netstat_test_zircon_deps.manifest") {
  visibility = [ ":*" ]
  manifest = "image"
  patterns = [
    "bin/ping",
  ]
}

test_package("netstack_gotests") {
  deprecated_shell = "//build"

  deps = [
    ":netstack_netstat_test_zircon_deps.manifest",
    ":netstack_connectivity_test",
    ":netstack_fidlconv_test",
    ":netstack_filter_test",
    ":netstack_link_eth_test",
    ":netstack_test",
    "ifconfig:netstack_ifconfig_test",
    "netstack_service_test",
    "netstat",
    "netstat:netstack_netstat_test",
    "util:netstack_util_test",
  ]

  extra = get_target_outputs(":netstack_netstat_test_zircon_deps.manifest")

  binaries = [
    {
      name = "netstack_service_test"
    },
    {
      name = "netstat"
    },
  ]

  tests = [
    {
      name = "netstack_connectivity_test"
      environments = [ qemu_env ]
    },
    {
      name = "netstack_fidlconv_test"
      environments = [ qemu_env ]
    },
    {
      name = "netstack_filter_test"
      environments = [ qemu_env ]
    },
    {
      name = "netstack_link_eth_test"
      environments = [ qemu_env ]
    },
    {
      name = "netstack_test"
      environments = [ qemu_env ]
    },
    {
      name = "netstack_util_test"
      environments = [ qemu_env ]
    },
    {
      name = "netstack_ifconfig_test"
      environments = [ qemu_env ]
    },
    {
      name = "netstack_netstat_test"
      environments = [ qemu_env ]
    },
  ]
}

go_library("netstack_lib") {
  name = "netstack"

  deps = [
    "//garnet/go/src/fidlext/fuchsia/hardware/ethernet:fidlext_fuchsia_hardware_ethernet_fake_lib($go_toolchain)",
    "//garnet/public/go/third_party:github.com/google/btree",
    "//garnet/public/go/third_party:github.com/google/go-cmp",
    "//garnet/public/go/third_party:github.com/google/netstack",
    "//garnet/public/go/third_party:github.com/pkg/errors",
    "//garnet/public/go/third_party:golang.org/x/net",
    "//garnet/public/lib/component/go/src/app",
    "//garnet/public/lib/syslog/go/src/syslog",
    "//sdk/fidl/fuchsia.net.filter($go_toolchain)",
    "//sdk/fidl/fuchsia.netstack($go_toolchain)",
    "//sdk/fidl/fuchsia.sys($go_toolchain)",
    "//zircon/public/fidl/fuchsia-device($go_toolchain)",
    "//zircon/public/fidl/fuchsia-hardware-ethernet($go_toolchain)",
    "//zircon/public/fidl/fuchsia-inspect($go_toolchain)",
    "//zircon/public/fidl/fuchsia-net($go_toolchain)",
    "//zircon/public/fidl/fuchsia-net-stack($go_toolchain)",
    "//zircon/public/fidl/fuchsia-posix-socket($go_toolchain)",
  ]
}

go_test("netstack_connectivity_test") {
  gopackages = ["netstack/connectivity"]
  deps = [
    ":netstack_lib",
  ]
}

go_test("netstack_fidlconv_test") {
  gopackages = ["netstack/fidlconv"]
  deps = [
    ":netstack_lib",
  ]
}

go_test("netstack_filter_test") {
  gopackages = ["netstack/filter"]
  deps = [
    ":netstack_lib",
  ]
}

go_test("netstack_link_eth_test") {
  gopackages = ["netstack/link/eth"]
  deps = [
    ":netstack_lib",
  ]
}

go_test("netstack_test") {
  gopackages = ["netstack"]
  deps = [
    ":netstack_lib",
  ]
}
