# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/package.gni")
import("//build/rust/rustc_binary.gni")
import("//build/test/test_package.gni")
import("//build/testing/environments.gni")

group("tests") {
  testonly = true

  deps = [
    ":shortcut",
    ":shortcut_test",
  ]
}

group("configs") {
  deps = [
    ":ui_sysmgr.config",
  ]
}

rustc_binary("bin") {
  name = "shortcut"
  with_unit_tests = true
  edition = "2018"

  deps = [
    "//garnet/public/lib/fidl/rust/fidl",
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-component",
    "//garnet/public/rust/fuchsia-syslog",
    "//garnet/public/rust/fuchsia-zircon",
    "//sdk/fidl/fuchsia.ui.input2:fuchsia.ui.input2-rustc",
    "//sdk/fidl/fuchsia.ui.shortcut:fuchsia.ui.shortcut-rustc",
    "//sdk/fidl/fuchsia.ui.views:fuchsia.ui.views-rustc",
    "//third_party/rust_crates:failure",
    "//third_party/rust_crates:futures-preview",
    "//third_party/rust_crates:pin-utils",
  ]
}

config_data("ui_sysmgr.config") {
  for_pkg = "sysmgr"
  sources = [
    "config/ui_sysmgr.config",
  ]
}

package("shortcut") {
  deps = [
    ":bin",
  ]

  binaries = [
    {
      name = "shortcut"
    },
  ]

  meta = [
    {
      path = rebase_path("meta/shortcut_manager.cmx")
      dest = "shortcut_manager.cmx"
    },
  ]
}

test_package("shortcut_test") {
  deps = [
    ":bin_test",
  ]

  tests = [
    {
      name = "shortcut_bin_test"
      environments = basic_envs
    },
  ]
}
