# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/rust/rustc_binary.gni")
import("//build/test/test_package.gni")

group("tests") {
  testonly = true

  deps = [
    ":ime_service",
    ":ime_test",
  ]
}

rustc_binary("bin") {
  name = "ime_service"
  with_unit_tests = true
  edition = "2018"

  deps = [
    "//garnet/lib/ui/text/common:text_common",
    "//garnet/public/lib/fidl/rust/fidl",
    "//garnet/public/rust/fuchsia-async",
    "//garnet/public/rust/fuchsia-component",
    "//garnet/public/rust/fuchsia-syslog",
    "//garnet/public/rust/fuchsia-zircon",
    "//sdk/fidl/fuchsia.ui.input:fuchsia.ui.input-rustc",
    "//sdk/fidl/fuchsia.ui.text:fuchsia.ui.text-rustc",
    "//sdk/fidl/fuchsia.ui.text.testing:fuchsia.ui.text.testing-rustc",
    "//third_party/rust_crates:failure",
    "//third_party/rust_crates:futures-preview",
    "//third_party/rust_crates:parking_lot",
    "//third_party/rust_crates:pin-utils",
    "//third_party/rust_crates:unicode-normalization",
    "//third_party/rust_crates:unicode-segmentation",
  ]
}

package("ime_service") {
  deps = [
    ":bin",
  ]

  binaries = [
    {
      name = "ime_service"
    },
  ]

  meta = [
    {
      path = rebase_path("meta/ime_service.cmx")
      dest = "ime_service.cmx"
    },
  ]
}

test_package("ime_test") {
  deps = [
    ":bin_test",
    "//garnet/bin/ui/text/test_suite:test_suite",
  ]

  tests = [
    {
      name = "ime_service_bin_test"
      environments = basic_envs
    },
  ]
}
