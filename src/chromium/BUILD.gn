# Copyright 2019 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config.gni")
import("//build/packages/prebuilt_package.gni")
import("//build/packages/prebuilt_test_manifest.gni")
import("//src/lib/icu/tzdata/icu_tzdata_config_data.gni")

group("chromium") {
  public_deps = [
    ":web_engine",
    ":web_runner",
  ]
}

group("web_engine") {
  deps = [
    ":web_engine_config",
    ":web_engine_pkg",
  ]
}

config_data("web_engine_config") {
  visibility = [ ":web_engine" ]

  for_pkg = "sysmgr"
  sources = [ rebase_path("config/web_context_provider.config") ]
}

prebuilt_package("web_engine_pkg") {
  visibility = [ ":web_engine" ]

  package_name = "web_engine"
  archive = "//prebuilt/third_party/chromium/arch/$target_cpu/web_engine.far"
  debug_archive =
      "//prebuilt/build_ids/$target_cpu/chromium/web_engine.symbols.tar.bz2"
}

group("web_runner") {
  deps = [
    ":web_runner_config",
    ":web_runner_pkg",
  ]
}

config_data("web_runner_config") {
  visibility = [ ":web_runner" ]
  for_pkg = "appmgr"
  sources = [ rebase_path("config/http_scheme_map.config") ]

  outputs = [ "scheme_map/http.config" ]
}

prebuilt_package("web_runner_pkg") {
  visibility = [ ":web_runner" ]

  package_name = "web_runner"
  archive = "//prebuilt/third_party/chromium/arch/$target_cpu/web_runner.far"
}

# Prebuilt FAR tests
test_env = [ nuc_env ]

group("chromium_tests_group") {
  testonly = true
  deps = [
    ":chromium_tests",
    ":tzdata_for_chromium_tests_base_unittests",
  ]
}

prebuilt_test_manifest("chromium_tests") {
  archive_dir = "//prebuilt/third_party/chromium_tests/arch/x64"
  package_name_prefix = "chromium_tests"
  environments = test_env
}

icu_tzdata_config_data("tzdata_for_chromium_tests_base_unittests") {
  for_pkg = "chromium_tests_base_unittests"
  data_version = "44"
  testonly = true
}

group("tests") {
  testonly = true

  deps = [
    ":chromium_tests_group",
    ":web_engine",
    ":web_runner",
    "web_runner_tests:tests",
    "//src/fonts:downstream_test_deps",
  ]
}
