# Copyright 2020 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/package.gni")

group("intl_services") {
  testonly = true
  public_deps = [
    ":pkg",
    ":tests",
  ]
}

group("small") {
  deps = [ "config:small" ]
}

group("tests") {
  testonly = true
  public_deps = [ "tests" ]
}

group("config") {
  public_deps = [ "config" ]
}

source_set("lib") {
  sources = [
    "run.cc",
    "run.h",
  ]

  deps = [
    "//sdk/fidl/fuchsia.settings",
    "//sdk/lib/sys/cpp",
    "//sdk/lib/syslog/cpp",
    "//src/lib/fxl:fxl",
    "//src/lib/intl/intl_property_provider_impl",
    "//zircon/system/ulib/async-loop:async-loop-cpp",
    "//zircon/system/ulib/async-loop:async-loop-default",
  ]
}

executable("intl_services_bin") {
  sources = [ "main.cc" ]

  deps = [
    ":lib",
    "//sdk/lib/syslog/cpp",
  ]
}

package("pkg") {
  package_name = "intl_services"
  deps = [ ":intl_services_bin" ]

  binaries = [
    {
      name = "intl_services_bin"
      dest = "intl_services"
    },
  ]

  meta = [
    {
      path = "meta/intl_services.cmx"
      dest = "intl_services.cmx"
    },
  ]

  resources = [
    {
      path = rebase_path("//third_party/icu/common/icudtl.dat")
      dest = "icudtl.dat"
    },
  ]
}
